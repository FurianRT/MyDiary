apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'org.jetbrains.kotlin.android.extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    buildToolsVersion = '29.0.2'
    defaultConfig {
        applicationId "com.furianrt.mydiary"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 69
        versionName "1.8.9"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "true")
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "false")
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'WEATHER_API_KEY', weatherApikey
        it.buildConfigField 'String', 'IMAGE_API_KEY', imageApikey
        it.buildConfigField 'String', 'PREFS_PASSWORD', prefsPassword
        it.buildConfigField 'String', 'SUPPORT_EMAIL', supportEmail
        it.buildConfigField 'String', 'SUPPORT_EMAIL_PASSWORD', supportEmailPassword
        it.buildConfigField 'String', 'MERCHANT_ID', merchantId
        it.buildConfigField 'String', 'LICENSE_KEY', licenseKey
        it.buildConfigField 'String', 'ITEM_PREMIUM_SKU', itemPremiumSku
        it.resValue("string", "GEO_API_KEY", geoApiKey)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Modules
    implementation project(path: ':fab')

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //Android
    implementation "androidx.appcompat:appcompat:$support_version"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.preference:preference:$support_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'com.google.android.material:material:1.1.0'

    //Layout for tags
    implementation 'com.google.android:flexbox:1.1.0'

    //Fingerprint
    implementation 'androidx.biometric:biometric:1.0.1'

    //Firebase
    implementation 'com.google.firebase:firebase-core:17.2.3'
    implementation 'com.google.firebase:firebase-storage:19.1.1'
    implementation 'com.google.firebase:firebase-firestore:21.4.1'
    implementation 'com.google.firebase:firebase-messaging:20.1.1'
    implementation 'com.google.firebase:firebase-auth:19.2.0'

    //Rx for Firebase
    implementation 'com.github.FrangSierra:RxFirebase:1.5.6'

    //Crash tracking
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    //ORM
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "androidx.room:room-compiler:$room_version"

    //JSON
    implementation 'com.google.code.gson:gson:2.8.6'

    //Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    //DI
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    //Play services
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    //Permissions
    implementation 'pub.devrel:easypermissions:1.3.0'

    //REST
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    //RecyclerView animators
    implementation 'jp.wasabeef:recyclerview-animators:3.0.0'

    //RecyclerView with expandable items
    implementation 'com.thoughtbot:expandablecheckrecyclerview:1.4'

    //Image loading
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation('com.github.bumptech.glide:recyclerview-integration:4.11.0') {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }

    //Image editor
    implementation 'com.github.yalantis:ucrop:2.2.3'

    //Image compressing
    implementation 'id.zelory:compressor:2.1.0'

    //Color picker
    implementation 'com.larswerkman:HoloColorPicker:1.5'

    //Color picker preference
    implementation 'com.jaredrummler:colorpicker:1.1.0'

    //DateTime
    implementation 'net.danlew:android.joda:2.10.3'

    //DateTime pickers
    implementation 'com.wdullaer:materialdatetimepicker:4.2.3'

    //Calendar
    implementation 'com.github.kizitonwose:CalendarView:0.1.2'

    //ImageView with zoom
    implementation 'com.jsibbold:zoomage:1.3.0'

    //Sending emails
    implementation 'com.sun.mail:android-mail:1.6.4'
    implementation 'com.sun.mail:android-activation:1.6.4'

    //In-App purchases
    implementation 'com.anjlab.android.iab.v3:library:1.1.0'

    //Uri to real path converter
    implementation 'com.github.HBiSoft:PickiT:0.1.7'

    //Panorama view for images
    implementation 'com.gjiazhe:PanoramaImageView:1.0'

    //Fading edges for views
    implementation'com.github.bosphere.android-fadingedgelayout:fadingedgelayout:1.0.0'

    //Debugging tool for network calls, database content, etc.
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
}
